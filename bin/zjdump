#!/usr/bin/env zsh
# Source: https://github.com/theslyprofessor/zellij-pane-tracker/blob/main/scripts/zjdump
# Dump Zellij pane content with full scrollback
# Usage: 
#   zjdump 2        # Dump terminal_2 by ID
#   zjdump Pane#2   # Dump pane by display name (from pane-tracker JSON)
#   zjdump          # Dump current focused pane

[[ -z "$ZELLIJ" ]] && { echo "Not in Zellij"; exit 1; }

# If no argument, dump current pane
if [[ -z "$1" ]]; then
    zellij action dump-screen --full /tmp/zjd-current.txt
    cat /tmp/zjd-current.txt
    exit 0
fi

# Parse argument - could be terminal ID or display name
target="$1"
pane_json="/tmp/zj-pane-names.json"

# Check if pane-tracker JSON exists
if [[ ! -f "$pane_json" ]]; then
    echo "Warning: Pane tracker not running ($pane_json missing)"
    echo "Falling back to terminal_${target}"
    target_id="terminal_${target}"
else
    if [[ "$target" =~ ^[0-9]+$ ]]; then
        target_id="terminal_${target}"
    elif [[ "$target" =~ ^terminal_ ]]; then
        target_id="$target"
    else
        # Search by display name in JSON
        target_id=$(jq -r --arg name "$target" '.panes | to_entries[] | select(.value == $name) | .key' "$pane_json" 2>/dev/null | grep "^terminal_" | head -1)
        if [[ -z "$target_id" ]]; then
            # Try partial match
            search_name=$(echo "$target" | sed 's/#/ #/g')
            target_id=$(jq -r --arg name "$search_name" '.panes | to_entries[] | select(.value | contains($name)) | .key' "$pane_json" 2>/dev/null | grep "^terminal_" | head -1)
        fi
        if [[ -z "$target_id" ]]; then
            echo "Pane '$target' not found in pane tracker"
            echo "Available panes:"
            jq -r '.panes | to_entries[] | select(.key | startswith("terminal_")) | "  \(.key): \(.value)"' "$pane_json"
            exit 1
        fi
    fi
fi

n="${target_id#terminal_}"
f="/tmp/zjd-${n}.txt"

# Check if already on target pane (fast path)
current_pane=$(zellij action list-clients 2>/dev/null | tail -1 | awk '{print $2}')
if [[ "$current_pane" == "$target_id" ]]; then
    zellij action dump-screen --full "$f"
    cat "$f"
    exit 0
fi

# Save current position to return
origin_tab=$(zellij action query-tab-names 2>/dev/null | head -1)
origin_pane="$current_pane"

# Get list of tabs
tabs=($(zellij action query-tab-names 2>/dev/null))

# Search across all tabs
found=0
for tab in "${tabs[@]}"; do
    zellij action go-to-tab-name "$tab" 2>/dev/null
    
    # Cycle through panes in this tab
    local first_pane=""
    for i in {1..10}; do
        current_pane=$(zellij action list-clients 2>/dev/null | tail -1 | awk '{print $2}')
        
        # Detect wrap-around
        [[ -z "$first_pane" ]] && first_pane="$current_pane"
        [[ "$current_pane" == "$first_pane" && $i -gt 1 ]] && break
        
        if [[ "$current_pane" == "$target_id" ]]; then
            zellij action dump-screen --full "$f"
            found=1
            break 2
        fi
        zellij action focus-next-pane
    done
done

# Return to original position
zellij action go-to-tab-name "$origin_tab" 2>/dev/null
for i in {1..10}; do
    current_pane=$(zellij action list-clients 2>/dev/null | tail -1 | awk '{print $2}')
    [[ "$current_pane" == "$origin_pane" ]] && break
    zellij action focus-next-pane
done

if [[ $found -eq 1 ]]; then
    cat "$f"
else
    echo "Pane $target_id not found"
    exit 1
fi
